# Update via
# bazel mod deps --lockfile_mode=update
# --lockfile_mode=refresh refreshes mutable fields
# Test via
# bazel mod deps --lockfile_mode=error
#
# See https://bazel.build/external/lockfile for more info
#
# File structure:
# - Module Definition
# - bazel_dep Listing
# - bazel_dep Notes
# - bazel_dep Overrides
# - bazel_dep Configuration
# - non-dep Changes
#
# Notes on this structure:
# - separated to keep the listing minimal
# - keep the dependencies sorted alphabetically

##########################################
# Module Definition
##########################################
##########################################
module(
    name = "mchristen",
    version = "",
)

##########################################
# bazel_dep Listing
##########################################
##########################################
# REMINDER: When adding dependencies, update the Notes section below and keep
# any overrides / configuration in their respective sections ordered alphabetically
# [BCR](https://registry.bazel.build/)
bazel_dep(name = "aspect_bazel_lib", version = "2.21.2")
bazel_dep(name = "aspect_rules_js", version = "2.8.2")
bazel_dep(name = "aspect_rules_lint", version = "1.10.2")
bazel_dep(name = "aspect_rules_py", version = "1.6.6")
bazel_dep(name = "bazel_skylib_gazelle_plugin", version = "1.8.2")
bazel_dep(name = "bazel_skylib", version = "1.8.2")
bazel_dep(name = "buildifier_prebuilt", version = "8.2.1")
bazel_dep(name = "build_stack_rules_proto", version = "4.1.2")
bazel_dep(name = "catch2", version = "3.11.0")
bazel_dep(name = "emboss", repo_name = "com_google_emboss")
bazel_dep(name = "freertos", version = "10.5.1.bcr.2")
bazel_dep(name = "gazelle", version = "0.47.0", repo_name = "bazel_gazelle")
bazel_dep(name = "hedron_compile_commands")
bazel_dep(name = "nanopb", version = "0.4.9.1", repo_name = "com_github_nanopb_nanopb")
bazel_dep(name = "pico-sdk", version = "2.0.0")
bazel_dep(name = "pigweed")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "protobuf", version = "33.1", repo_name = "com_google_protobuf")
bazel_dep(name = "rules_cc", version = "0.2.14")
bazel_dep(name = "rules_multirun", version = "0.13.0")
bazel_dep(name = "rules_multitool", version = "1.11.1")
bazel_dep(name = "rules_mypy", version = "0.40.0")
bazel_dep(name = "rules_oci", version = "2.2.6")
bazel_dep(name = "rules_platform", version = "0.1.0")
bazel_dep(name = "rules_proto", version = "7.1.0")
bazel_dep(name = "rules_python_gazelle_plugin", version = "1.7.0-rc4")
bazel_dep(name = "rules_python", version = "1.7.0")
bazel_dep(name = "rules_rust")
bazel_dep(name = "rules_shell", version = "0.6.1")
bazel_dep(name = "rules_uv", version = "0.88.0")
bazel_dep(name = "toolchains_llvm", version = "1.5.0")

##########################################
# bazel_dep Notes
##########################################
##########################################
# (NOTE: We don't put the follow "Repo Notes" inline with the bazel_deps in
# order to keep them clean / immediately viewable)

# Repo Notes:
# - aspect_bazel_lib: https://registry.bazel.build/modules/aspect_bazel_lib
# - aspect_rules_js: https://registry.bazel.build/modules/aspect_rules_js
#   - NOTE: We don't really use/support JS in this repo, and if you need to
#   you'll have some more config to do, but we register rules_js here so we can
#   use it to pull a few javascript packages used for repo tooling, like
#   prettier for linting.
#   - OPTIONAL: Customize Node.js version #########
#      By default you get the node version from DEFAULT_NODE_VERSION in @rules_nodejs//nodejs:repositories.bzl
#      Optionally you can pin a different node version:
#      bazel_dep(name = "rules_nodejs", version = "5.8.2")
#      node = use_extension("@rules_nodejs//nodejs:extensions.bzl", "node")
#      node.toolchain(node_version = "16.14.2")
# - aspect_rules_lint: https://registry.bazel.build/modules/aspect_rules_lint
#   - Next, follow the install instructions for
#     - linting: https://github.com/aspect-build/rules_lint/blob/v1.0.3/docs/linting.md
#     - formatting: https://github.com/aspect-build/rules_lint/blob/v1.0.3/docs/formatting.md
# - aspect_rules_py: https://registry.bazel.build/modules/aspect_rules_py
# - bazel_skylib_gazelle_plugin: https://registry.bazel.build/modules/bazel_skylib_gazelle_plugin
# - bazel_skylib: https://registry.bazel.build/modules/bazel_skylib
#   - Bazel rule helpers, including testing
# - buildifier_prebuilt:
# https://registry.bazel.build/modules/buildifier_prebuilt
#   - Set up hermetic Buildifier tools
#   - This is helpful because the old version used an http_archive to fetch the buildtools
#     WORKSPACE, but it is incompatible with bzlmod Go toolchains. Instead let's just use
#     a prebuilt version.
# - build_stack_rules_proto: https://registry.bazel.build/modules/build_stack_rules_proto
# - catch2: https://registry.bazel.build/modules/catch2
#   - C++ Test framework
#   - https://github.com/catchorg/Catch2
#   - Consider adding listeners via https://github.com/catchorg/Catch2/blob/devel/docs/event-listeners.md
#   - Consider the googletest alternative: https://google.github.io/googletest/
#     - bazel_dep(name = "googletest", version = "1.15.2")
# - emboss: https://pigweed.googlesource.com/third_party/github/google/emboss
# - freertos
# - gazelle: https://github.com/bazel-contrib/bazel-gazelle
# - hedron_compile_commands
# - nanopb
# - pico-sdk
# - pigweed: https://pigweed.dev/get_started/bazel_integration/setup.html
#   - Example repo: https://cs.opensource.google/pigweed/quickstart/bazel
#   - Not available in bcr at the moment
#   - This is a LARGE framework, which does a lot of heavy lifting for us, but
#   also buys us into several other dependencies, etc. and establishes limits
#   on settings we take, eg) there is currently a [C++17
#   limitation](https://pigweed.dev/style/cpp.html#c-standard)
#   - Causes us to pull in several other dependencies (we've chosen the
#   versions to match what pigweed uses)
#     - freertos
#     - nanopb
#     - pico-sdk
#     - platforms
#     - rules_platform
#     - There are others, which we use separately:
#       - bazel_skylib
#       - hedron_compile_commands
#       - rules_cc
#       - rules_mypy
#       - rules_python
#   - See
#   https://cs.opensource.google/pigweed/quickstart/bazel/+/main:MODULE.bazel
#   for recommendations on sub-dependencies
# - platforms: https://registry.bazel.build/modules/platforms
# - protobuf: https://registry.bazel.build/modules/protobuf
# - rules_cc: https://registry.bazel.build/modules/rules_cc
# - rules_multirun: https://registry.bazel.build/modules/rules_multirun
#   - allow parallel invocations (see lint.sh)
# - rules_multitool: https://registry.bazel.build/modules/rules_multitool
# - rules_mypy: https://registry.bazel.build/modules/rules_mypy
# - rules_oci: https://registry.bazel.build/modules/rules_oci
# - rules_platform: https://registry.bazel.build/modules/rules_platform
#   - Allow platform transitions
# - rules_proto: https://registry.bazel.build/modules/rules_proto
#   - https://github.com/stackb/rules_proto
# - rules_python_gazelle_plugin: https://registry.bazel.build/modules/rules_python_gazelle_plugin
#   - Note this is a sub-directory of the rules_python repo
# - rules_python: https://registry.bazel.build/modules/rules_python
#   - https://github.com/bazelbuild/rules_python
#   - See #55
# - rules_rust: https://registry.bazel.build/modules/rules_rust
# - rules_shell: https://registry.bazel.build/modules/rules_shell
# - rules_uv: https://registry.bazel.build/modules/rules_uv
# - toolchains_llvm: https://registry.bazel.build/modules/toolchains_llvm

# Considerations for the future
#
# TODO: Should add buf too
#
# TODO(#78): Consider re-enabling
# Alternative to gcc-toolchain: https://github.com/uber/hermetic_cc_toolchain
# http_archive(
#     name = "aspect_gcc_toolchain",
#     integrity = "sha256-iqcSkkfwbhKrNWeX957qE/I4fzKuj3GEB06OZAJ5Apk=",
#     strip_prefix = "gcc-toolchain-0.6.0",
#     urls = [
#         "https://github.com/f0rmiga/gcc-toolchain/archive/refs/tags/0.6.0.tar.gz",
#     ],
# )
#
# load("@aspect_gcc_toolchain//toolchain:repositories.bzl", "gcc_toolchain_dependencies")
#
# gcc_toolchain_dependencies()
#
# load("@aspect_gcc_toolchain//toolchain:defs.bzl", "ARCHS", "gcc_register_toolchain")
#
# gcc_register_toolchain(
#     name = "gcc_toolchain_x86_64",
#     target_arch = ARCHS.x86_64,
# )

##########################################
# bazel_dep Overrides
##########################################
##########################################
# (NOTE: We don't put the overrides in-line with the bazel_dep for readability)
# order to keep them clean / immediately viewable)

git_override(
    module_name = "emboss",
    commit = "f595908cba6f366746d6834a2e885868d1a836cd",
    # LINT.IfChange(emboss)
    remote = "https://pigweed.googlesource.com/third_party/github/google/emboss",
    # tag = "v2024.0809.170004",
    # LINT.ThenChange(/pw_package/py/pw_package/packages/emboss.py:emboss)
)

# ================
# TODO: https://pwbug.dev/349880767 - Point this back to the upstream repo once this PR is merged.
archive_override(
    module_name = "hedron_compile_commands",
    strip_prefix = "bazel-compile-commands-extractor-163521345aa6366fd1ed801b989b668b5c806f69",
    urls = ["https://github.com/chadnorvell/bazel-compile-commands-extractor/archive/163521345aa6366fd1ed801b989b668b5c806f69.tar.gz"],
)

git_override(
    module_name = "pigweed",
    # ROLL: Warning: this entry is automatically updated by
    # ROLL: https://cr-buildbucket.appspot.com/builder/pigweed/quickstart.roll/pigweed-quickstart-bazel-roller
    commit = "6e6d54426b139c4044dc9a5ccf635e1531ca16f2",
    remote = "https://pigweed.googlesource.com/pigweed/pigweed",
)

archive_override(
    module_name = "rules_rust",
    integrity = "sha256-+bWb47wg0VchIADaHt6L5Dma2Gn+Q589nz/MKcTi+lo=",
    # Can only apply patches from local workspace
    # patch_strip = 1,
    # patches = [
    #     # Fix rustdoc test w/ proc macros
    #     # https://github.com/bazelbuild/rules_rust/pull/1952
    #     "@pigweed//pw_rust/bazel_patches:0001-rustdoc_test-Apply-prefix-stripping-to-proc_macro-de.patch",
    #     # Adds prototype functionality for documenting multiple crates in one
    #     # HTML output directory.  While the approach in this patch may have
    #     # issues scaling to giant mono-repos, it is appropriate for embedded
    #     # projects and minimally invasive and should be easy to maintain.  Once
    #     # the `rules_rust` community decides on a way to propperly support this,
    #     # we will migrate to that solution.
    #     # https://github.com/konkers/rules_rust/tree/wip/rustdoc
    #     "@pigweed//pw_rust/bazel_patches:0002-PROTOTYPE-Add-ability-to-document-multiple-crates-at.patch",
    # ],
    urls = ["https://github.com/bazelbuild/rules_rust/releases/download/0.45.1/rules_rust-v0.45.1.tar.gz"],
)

##########################################
# bazel_dep Configuration
##########################################
##########################################

##########
# pigweed
##########
# Register Pigweed's C++ toolchains.
register_toolchains(
    "@pigweed//pw_toolchain:cc_toolchain_cortex-m0",
    "@pigweed//pw_toolchain:cc_toolchain_cortex-m33",
    "@pigweed//pw_toolchain/host_clang:host_cc_toolchain_linux",
    "@pigweed//pw_toolchain/host_clang:host_cc_toolchain_macos",
    dev_dependency = True,
)

###########
# rules_js
###########
npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm", dev_dependency = True)

pnpm = use_extension("@aspect_rules_js//npm:extensions.bzl", "pnpm", dev_dependency = True)

npm.npm_translate_lock(
    name = "npm",
    pnpm_lock = "//:pnpm-lock.yaml",
    verify_node_modules_ignored = "//:.bazelignore",
)
use_repo(npm, "npm")

use_repo(pnpm, "pnpm")

##################
# rules_multitool
##################
multitool = use_extension("@rules_multitool//multitool:extension.bzl", "multitool")
multitool.hub(lockfile = "//:multitool.lock.json")
use_repo(multitool, "multitool")

#############
# rules_mypy
#############
types = use_extension("@rules_mypy//mypy:types.bzl", "types")
types.requirements(
    name = "pip_types",
    # `@pip` in the next line corresponds to the `hub_name` when using
    # rules_python's `pip.parse(...)`.
    pip_requirements = "@pip//:requirements.bzl",
    # also legal to pass a `requirements.in` here
    requirements_txt = "//:requirements_lock.txt",
)
use_repo(types, "pip_types")

############
# rules_oci
############
oci = use_extension("@rules_oci//oci:extensions.bzl", "oci")
oci.pull(
    name = "distroless_base",
    # 'latest' is not reproducible, but it's convenient.
    # During the build we print a WARNING message that includes recommended 'digest' and 'platforms'
    # values which you can use here in place of 'tag' to pin for reproducibility.
    # tag = "latest",
    digest = "sha256:9d4e5680d67c984ac9c957f66405de25634012e2d5d6dc396c4bdd2ba6ae569f",
    image = "gcr.io/distroless/base",
    platforms = [
        "linux/amd64",
        "linux/arm64/v8",
    ],
)
oci.pull(
    name = "ubuntu_base",
    # tag = "24.04",
    digest = "sha256:723ad8033f109978f8c7e6421ee684efb624eb5b9251b70c6788fdb2405d050b",
    image = "ubuntu",
    platforms = [
        "linux/amd64",
        "linux/arm64/v8",
    ],
)

# For each oci.pull call, repeat the "name" here to expose them as dependencies.
use_repo(oci, "distroless_base", "distroless_base_linux_amd64", "distroless_base_linux_arm64_v8", "ubuntu_base", "ubuntu_base_linux_amd64", "ubuntu_base_linux_arm64_v8")

###############
# rules_python
###############
# Minimum version needs:
# feat: add interpreter_version_info to py_runtime by @mattem in #1671

python = use_extension("@rules_python//python/extensions:python.bzl", "python")

# Must match interpreter_version in .bazelrc
# & mypy_cli in //:BUILD
# LINK(7e463bc3_e4d9_4464_ba39_3217c4a86004)
PYTHON_VERSION = "3.11.9"

python.toolchain(
    configure_coverage_tool = True,
    python_version = PYTHON_VERSION,
)

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
pip.parse(
    # TODO(#209)
    # Avoid building wheels with system compiler
    # - pygraphviz
    # download_only = True,
    # Could put --extra-index-url here to grab my own wheels
    # "--prefer-binary",
    # "--only-binary=pygraphviz",
    # extra_pip_args = [],
    # TODO(#209): clang may not be available when building wheels, use clang
    # instead (fix by not building here)
    environment = {"CC": "gcc"},
    # Avoid building wheels with system compiler
    # - pygraphviz
    # download_only = True,
    # Could put --extra-index-url here to grab my own wheels
    # extra_pip_args = [],
    # https://rules-python.readthedocs.io/en/latest/pypi-dependencies.html
    experimental_requirement_cycles = {
        "openhtf": [
            "openhtf",
            # specify undeclared dependency on six
            "six",
        ],
        "networkx": [
            "networkx",
            "pygraphviz",
        ],
    },
    hub_name = "pip",
    python_version = PYTHON_VERSION,
    requirements_lock = "//:requirements_lock.txt",
)

# Could support multi-platform by specifying a different target for each
# platform
# --platform
# --python-version
# --implementation
# --abi
use_repo(pip, "pip")

#############
# rules_rust
#############
rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(edition = "2021")
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

# XXX: bazel mod tidy fails, likely related to this
crate = use_extension(
    "@rules_rust//crate_universe:extension.bzl",
    "crate",
)
crate.from_cargo(
    name = "crate_index",
    cargo_lockfile = "//:Cargo.lock",
    manifests = [
        "//:examples/basic/Cargo.toml",
    ],
)
use_repo(crate, "crate_index")

##################
# toolchains_llvm
##################
llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm")
llvm.toolchain(
    llvm_versions = {
        "": "16.0.0",
        "darwin-x86_64": "15.0.7",
    },
)
use_repo(llvm, "llvm_toolchain", "llvm_toolchain_llvm")

##########################################
# non-dep Changes
##########################################
##########################################

# Register our own toolchains
register_toolchains(
    "//examples/bazel:foo_rude_toolchain",
    "//examples/bazel:foo_nice_toolchain",
)
