# Copyright 2020 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pigweed/targets/stm32f429i_disc1/target_toolchains.gni")
import("$dir_pw_protobuf_compiler/proto.gni")
import("$dir_pw_third_party/nanopb/nanopb.gni")

_toolchain_args = {
  # Configure backend for assert facade.
  pw_assert_BACKEND = dir_pw_assert_basic

  # Configure the pw_log facade for Base64 tokenized logging.
  pw_log_BACKEND = dir_pw_log_tokenized
  pw_log_tokenized_HANDLER_BACKEND = "$dir_pw_log_tokenized:base64_over_hdlc"

  # Alternately, configure pw_log for plain text logging
  # pw_log_BACKEND = dir_pw_log_basic

  pw_build_LINK_DEPS = [
    "$dir_pw_assert:impl",
    "$dir_pw_log:impl",
  ]

  pw_board_led_BACKEND = "//libraries/pw_board_led_stm32f429i_disc1"
}

target_toolchain_stm32f429i_disc1 = {
  _excluded_members = [
    "defaults",
    "name",
  ]
  _excluded_defaults = [
    "pw_cpu_exception_ENTRY_BACKEND",
    "pw_cpu_exception_HANDLER_BACKEND",
    "pw_cpu_exception_SUPPORT_BACKEND",
  ]

  debug = {
    name = "stm32f429i_disc1_debug"
    _toolchain_base = pw_target_toolchain_stm32f429i_disc1.debug
    forward_variables_from(_toolchain_base, "*", _excluded_members)
    defaults = {
      forward_variables_from(_toolchain_base.defaults, "*", _excluded_defaults)
      forward_variables_from(_toolchain_args, "*")
    }
  }

  # Toolchain for tests only.
  debug_tests = {
    name = "stm32f429i_disc1_debug_tests"
    _toolchain_base = pw_target_toolchain_stm32f429i_disc1.debug
    forward_variables_from(_toolchain_base, "*", _excluded_members)
    defaults = {
      forward_variables_from(_toolchain_base.defaults, "*", _excluded_defaults)
      forward_variables_from(_toolchain_args, "*")

      # Force tests to use basic log backend to avoid generating and loading its
      # own tokenized database.
      pw_log_BACKEND = dir_pw_log_basic
    }
  }
}

toolchains_list = [
  target_toolchain_stm32f429i_disc1.debug,
  target_toolchain_stm32f429i_disc1.debug_tests,
]
